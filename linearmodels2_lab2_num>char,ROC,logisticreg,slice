Data Neuralgia;
      input Treatment $ Sex $ Age Duration Pain $ @@;
      datalines;
   P  F  68   1  No   B  M  74  16  No  P  F  67  30  No
   P  M  66  26  Yes  B  F  67  28  No  B  F  77  16  No
   A  F  71  12  No   B  F  72  50  No  B  F  76   9  Yes
   A  M  71  17  Yes  A  F  63  27  No  A  F  69  18  Yes
   B  F  66  12  No   A  M  62  42  No  P  F  64   1  Yes
   A  F  64  17  No   P  M  74   4  No  A  F  72  25  No
   P  M  70   1  Yes  B  M  66  19  No  B  M  59  29  No
   A  F  64  30  No   A  M  70  28  No  A  M  69   1  No
   B  F  78   1  No   P  M  83   1  Yes B  F  69  42  No
   B  M  75  30  Yes  P  M  77  29  Yes P  F  79  20  Yes
   A  M  70  12  No   A  F  69  12  No  B  F  65  14  No
   B  M  70   1  No   B  M  67  23  No  A  M  76  25  Yes
   P  M  78  12  Yes  B  M  77   1  Yes B  F  69  24  No
   P  M  66   4  Yes  P  F  65  29  No  P  M  60  26  Yes
   A  M  78  15  Yes  B  M  75  21  Yes A  F  67  11  No
   P  F  72  27  No   P  F  70  13  Yes A  M  75   6  Yes
   B  F  65   7  No   P  F  68  27  Yes P  M  68  11  Yes
   P  M  67  17  Yes  B  M  70  22  No  A  M  65  15  No
   P  F  67   1  Yes  A  M  67  10  No  P  F  72  11  Yes
   A  F  74   1  No   B  M  80  21  Yes A  F  69   3  No
   ;

*************************************************************
Examples relative to HW1
************************************************************;
   *check variable types; 
proc contents data=neuralgia; run;

**convert old numeric variable (duration) to new character variable (new_duration);
data example; 
set neuralgia;
new_duration=put(duration, $4.);
run; 
*check variable types; 
proc contents data=example; run;
*view results; 
proc print data=example; run;

*convert old character variable (new_duration) to numeric (newest_duration);
data example;
set example; 
newest_duration=input(new_duration, 4.);
run;
*check variable types; 
proc contents data=example; run;
*view results; 
proc print data=example; run;

************************************************************;

*logistic model with single continuous predictor; 
proc logistic data=neuralgia ; 
model pain(ref='No')=Age/scale=none aggregate;
run;

*as above, but added Hosmer-Lemeshow test with lackfit option; 
proc logistic data=neuralgia ; 
model pain(ref='No')=Age/scale=none aggregate lackfit;
run;

*still as above, but request odds ratio corresponding to 5-year change in age;
proc logistic data=neuralgia ; 
model pain(ref='No')=Age/scale=none aggregate lackfit clodds=wald;
units age=5;
run;

*create 2-level treatment variable called "Treat"; 
data neuralgia;
set neuralgia; 
if treatment='A' or Treatment='B' then Treat=1; else Treat=0; 
run;

*logistic model with dichotomous predictor; 
proc logistic data=neuralgia;
class Treat(ref='0')/param=reference; 
model pain(ref='No')=Treat;
run;

*logistic model with multi-level categorical predictor; 
*contrast statement to compare levels A vs B;
proc logistic data=neuralgia;
class Treatment(ref='P')/param=reference; 
model pain(ref='No')= Treatment;
contrast 'Treatment A vs. B' treatment 1 -1/estimate=exp;
run;

*logistic model with two (categorical) predictors;
proc logistic data=neuralgia;
class Treat(ref='0') Sex(ref='F');
model pain(ref='No') = Treat Sex /scale=none aggregate;
run;
 
*multivariable logistic model; 
proc logistic data=neuralgia; 
class Treatment(ref='P') Sex(ref='F')/param=reference;
model pain(ref='No') = Treatment Sex Age Duration/scale=none aggregate; 
run; 

*multivariable logistic model with treatment x sex interaction; 
proc logistic data=neuralgia; 
class Treatment(ref='P') Sex(ref='F')/param=reference;
model pain(ref='No') = Treatment Sex Age Duration Treatment*Sex/scale=none aggregate; 
run; 

*as above, with some nice plots; 
proc logistic data=neuralgia; 
class Treatment(ref='P') Sex(ref='F')/param=reference;
model pain(ref='No') = Treatment Sex Age Duration Treatment*Sex/scale=none aggregate; 
effectplot / at(Sex=all) noobs;
effectplot slicefit(sliceby=Sex plotby=Treatment) / noobs;
run;


*multivariable logistic model with lsmeans slice effects to probe treatment x sex interaction;
*sliceby specifies the stratification, diff requests comparisons, oddsratio and cl requests ORs 
*and confidenec limits, adjust=bon specifies a bonferroni adjustment for multiple comparisons; 
proc logistic data=neuralgia; 
class Treatment(ref='P') Sex(ref='F')/param=reference;
model pain(ref='No') = Treatment Sex Age Duration Treatment*Sex/scale=none aggregate; 
slice treatment*sex/sliceby=sex diff oddsratio CL adjust=bon;
run; 



*a (forward) stepwise-selection logistic model in the neuralgia data;
*the slentry=0.2 tells it to only allow variables with p-values <0.2 to enter the model;
*the slstay specifies that only variables with p-values <0.15 to be retained in final models;  
proc logistic data=neuralgia; 
class Treatment(ref='P') Sex(ref='F')/param=reference;
model pain(ref='No') = Treatment Sex Age Duration Treatment*Sex/selection=stepwise slentry=0.2 slstay=0.15 details lackfit;  
run; 


*a backwards elimination stepwise logistic model in the neuralgia data;
*the slentry=0.2 tells it to only allow variables with p-values <0.2 to enter the model;
*the slstay specifies that only variables with p-values <0.15 to be retained in final models;  
proc logistic data=neuralgia; 
class Treatment(ref='P') Sex(ref='F')/param=reference;
model pain(ref='No') = Treatment Sex Age Duration Treatment*Sex/selection=backward slentry=0.2 slstay=0.15 details lackfit;  
run; 

*call a multivariable model with ROCs for varying combinations of predictor variables; 
proc logistic data=neuralgia descending;
	class Sex(ref='F') Treatment(ref='P') /param=reference;
	model pain(ref='No') = Age Sex Duration Treatment /scale=none ctable;
	roc;
	roc 'Treat' Treatment;
	roc 'Treat,Age' Treatment Age;
	roc 'Treat,Sex' Treatment Sex;
run;

*calling regression/influence diagnostics;
proc logistic data=neuralgia;
class Sex(ref='F') Treatment(ref='P') /param=reference;
model pain(ref='No') = Age Sex Duration Treatment /influence;
run;

